## This expands for almost all blocks (blocks/expander chooses which)
execute if entity @e[tag=soul_home,distance=..1] run say expand_generic

## Sand blocks will fall if they aren't supported, so we place barriers under them
execute if block ~0 ~3 ~0 #restworld:sand run fill ~-1 ~2 ~-1 ~1 ~2 ~1 barrier replace air

<%doc>We want to clone up the snow topper, if it exists. If it doesn't, we need that layer
      to be cleared (it might have something from a previous expansion). And snow is the
      only thing that is normally placed on that level. </%doc>
execute unless block ~0 ~4 ~0 snow run fill ~-1 ~4 ~-1 ~1 ~4 ~1 air

## Clone the original block to either side to form a line, including anything on top of the block
clone ~0 ~4 ~0 ~0 ~3 ~0 ~-1 ~3 ~0
clone ~0 ~4 ~0 ~0 ~3 ~0 ~1 ~3 ~0

## Clone the line to either side to get a 3x3 level
clone ~1 ~4 ~0 ~-1 ~3 ~0 ~-1 ~3 ~-1
clone ~1 ~4 ~0 ~-1 ~3 ~0 ~-1 ~3 ~1

## Clone the fill bottom level to the top
clone ~1 ~4 ~1 ~-1 ~3 ~-1 ~-1 ~5 ~-1

## Soil needs the middle level filled with dirt
execute if block ~0 ~5 ~0 #restworld:soil run fill ~-1 ~3 ~-1 ~1 ~4 ~1 dirt

## Otherwise fill the middle level with the top level
execute unless block ~0 ~5 ~0 #restworld:soil run clone ~1 ~5 ~1 ~-1 ~5 ~-1 ~-1 ~4 ~-1
